#compdef jiagubao

_commands=(
    '-help:显示帮助信息.'
    '-version:显示当前版本号.'
    '-update:升级到最新版本.'
    '-login:使用360账户登陆。必须先登录才能加固.'
    '-importsign:导入签名信息.'
    '-config:配置加固可选项.'
    '-config_so:配置需要加固的SO文件，以空格分隔.'
    '-config_so_private:配置防盗用的SO文件，以空格分隔.'
    '-config_assets:配置需要忽略的资源文件，以空格分隔.'
    '-jiagu:加固.'
    '-showconfig:显示已配置加固项.'
    '-showsign:查看已配置的签名信息.'
    '-showmulpkg:查看已配置的多渠道信息.'
    '-deletemulpkg:清除已配置的多渠道信息.'
    '-importmulpkg:导入多渠道配置信息（txt格式）'
)

function _jiagubao_commands() {
    _describe 'command' _commands
}

function _jiagubao() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :_jiagubao_commands' \
        '*:: :->option'

    case $state in
        option)
            case $words[1] in
                -importmulpkg|-importsign)
                    _arguments ':files:_files'
                    ;;
                -jiagu)
                    _arguments \
                        ':inputApkFile:{_files -g "*.apk"}' \
                        ':outputApkFile:{_files -g "*.apk"}' \
                        '-autosign[自动签名]' \
                        '-automulpkg[自动多渠道]' \
                        '-pkgparam[自定义文件生成多渠道]:pkgparam:_files'
                    ;;
                -config)
                    _arguments \
                        '-crashlog[崩溃日志分析]' \
                        '-x86[x86支持]' \
                        '-analyse[加固数据分析]' \
                        '-nocert[跳过签名校验]' \
                        '-piracy[盗版监测]' \
                        '-vmp[全VMP保护]' \
                        '-data[本地数据文件保护]' \
                        '-assets[资源文件保护]' \
                        '-filecheck[文件完整性校验]' \
                        '-ptrace[Ptrace防注入]' \
                        '-so[SO文件保护]' \
                        '-dex2c[dex2C保护]' \
                        '-string_obfus[字符串加密]' \
                        '-dex_shadow[DexShadow]' \
                        '-so_private[SO防盗用]' \
                        '-double_check[双开检测]'
                    ;;
                *);;
            esac
            ;;
        *);;
    esac
}

_jiagubao "$@"
